; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Password Tech"
#define MyAppVersion "3.5.2"
#define MyAppPublisher "Christian Thöing"
#define MyAppURL "http://pwgen-win.sourceforge.net"
#define MyAppExeName "PwTech.exe"
#define MySetupImageIcon "c:\projekte\pwgen3\graphics\pwtech.ico"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{F2074AC4-1897-470C-A160-EF5C74F76763}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=C:\Projekte\PWGen3\license.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputBaseFilename=PwTech-{#MyAppVersion}-Setup
Compression=lzma2
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64
SignTool=mssign
SetupIconFile={#MySetupImageIcon}
UninstallDisplayIcon={app}\{#MyAppExeName}

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[CustomMessages]
english.OpenChangesTxt=View "changes.txt" to learn more about new features in this version
english.DontUseAppDataPath=Store program settings ("PwTech.ini") and randseed file in program folder instead of %APPDATA% folder
english.Donate=Support the development of Password Tech by making a donation (via PayPal)
german.OpenChangesTxt=Datei "changes.txt" (englisch) öffnen, um mehr über neue Features in dieser Programmversion zu erfahren
german.DontUseAppDataPath=Programmeinstellungen ("PwTech.ini") und Randseed-Datei im Programmordner statt im %APPDATA%-Ordner speichern
german.Donate=Die Entwicklung von Password Tech mit einer Spende unterstützen (via PayPal) 

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "useprogfolder"; Description: "{cm:DontUseAppDataPath}"; Flags: unchecked

[Files]
Source: "C:\Projekte\PWGen3\Release\64bit\PwTech.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: Is64BitInstallMode
Source: "C:\Projekte\PWGen3\Release\64bit\PwTech.com"; DestDir: "{app}"; DestName: "PwTech.com"; Flags: ignoreversion; Check: Is64BitInstallMode
Source: "C:\Projekte\PWGen3\Release\32bit\PwTech.exe"; DestDir: "{app}"; Flags: ignoreversion solidbreak; Check: not Is64BitInstallMode
Source: "C:\Projekte\PWGen3\Release\32bit\PwTech.com"; DestDir: "{app}"; Flags: ignoreversion; Check: not Is64BitInstallMode
Source: "C:\Projekte\PWGen3\script_examples\*"; DestDir: "{app}\script_examples"; Flags: ignoreversion solidbreak
Source: "C:\Projekte\PWGen3\manual\manual.pdf"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Projekte\PWGen3\manual\scripting.pdf"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Projekte\PWGen3\license.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Projekte\PWGen3\common_passwords.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Projekte\PWGen3\changes.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Projekte\PWGen3\Win64\Release\German.po"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{group}\User Manual"; Filename: "{app}\manual.pdf"
Name: "{group}\Scripting Manual"; Filename: "{app}\scripting.pdf"

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "{app}\changes.txt"; Description: "{cm:OpenChangesTxt}"; Flags: postinstall shellexec skipifsilent unchecked
Filename: "https://www.paypal.com/donate/?hosted_button_id=XLLCYFJZLS5V8"; Description: "{cm:Donate}"; Flags: postinstall shellexec skipifsilent unchecked

[INI]
Filename: "{app}\PwTech.ini"; Section: "Main"; Key: "UseAppDataPath"; String: "1"; Tasks: not useprogfolder
Filename: "{app}\PwTech.ini"; Section: "Main"; Key: "Language"; String: {code:MyLang|{language}}

[InstallDelete]


[UninstallDelete]
Type: files; Name: "{app}\PwTech.ini"
;Type: files; Name: "{userappdata}\{#MyAppName}\PwTech.ini"; Tasks: not useprogfolder
Type: files; Name: "{app}\randseed.dat"; Tasks: useprogfolder
Type: files; Name: "{userappdata}\{#MyAppName}\randseed.dat"; Tasks: not useprogfolder

[Code]
function MyLang(Param: String): String;
begin
  if CompareText(Param, 'german') = 0 then begin
    Result := 'de';
  end
  else begin
    Result := Param;
  end;
end;